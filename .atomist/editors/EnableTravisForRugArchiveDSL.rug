/*
 * Copyright Â© 2016 Atomist, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

@description "Enable Travis CI for a Rug Archive project (Rug DSL version)"
@tag "travis-ci"
@tag "continuous-integration"
@tag "github"
@tag "rug"
editor EnableTravisForRugArchiveDSL

@displayName "Repo Slug"
@description "GitHub repo slug of the form 'owner/repo'"
@validInput "An existing Github repository slug of the form 'owner/repo', must be 3-100 characters long"
@minLength 3
@maxLength 100
param repo_slug: ^[-\w.]+/[-\w.]+$

@displayName "Travis CI Endpoint"
@description "Specify the Travis CI .org or .com endpoint"
@validInput "Either '.org' or '.com'"
@minLength 4
@maxLength 4
@default '.org'
param org: ^\.(org|com)$

@displayName "GitHub Token"
@description "GitHub Personal Access Token of the repo owner with the following scopes: repo, write:repo_hook, user:email, and read:org, generated at https://github.com/settings/tokens; if the repo owner is an organization, the token must belong to a user in the Owner group of the organization"
@validInput "A valid 40-character, lower-case hexadecimal GitHub Personal Access token"
@minLength 40
@maxLength 40
param github_token: ^[a-f0-9]{40}$

@displayName "Maven Base URL"
@description "The URL for a Maven-compatible repository, without the trailing repository name, e.g., if the Maven repository URL is 'https://atomist.jfrog.io/atomist/rugs-release', then you would provide 'https://atomist.jfrog.io/atomist' for this value"
@validInput "A valid URL starting with http, https, or ftp, must be 7-100 characters long"
@minLength 7
@maxLength 100
@default "https://atomist.jfrog.io/atomist"
param maven_base_url: @url

@displayName "Maven User"
@description "Maven user with write access to the rugs-dev and rugs-release repositories under the Maven Base URL"
@validInput "A valid user name for the rugs-dev and rugs-release Maven repository under the Maven Base URL, must be 1-100 characters long"
@minLength 1
@maxLength 100
param maven_user: ^.*$

@displayName "Maven Token"
@description "API token or password for the Maven User"
@validInput "A valid user authentication token, i.e., API token or password, for Maven User, must be 1-100 characters long"
@minLength 1
@maxLength 100
param maven_token: ^.*$

let buildDir = ".atomist/build"
let travisBuild = "travis-build.bash"
let cliBuild = "cli-build.yml"
let cliRelease = "cli-release.yml"
let cliDev = "cli-dev.yml"

with Project p when directoryExists ".atomist"
  begin
    do merge "travis.yml-rug.vm" to ".travis.yml"

    do deleteFile travisBuild
    do addDirectoryAndIntermediates buildDir
    do merge { travisBuild + "-rug.vm" } to { buildDir + "/" + travisBuild }
    do merge { cliBuild + ".vm" } to { buildDir + "/" + cliBuild }
    do merge { cliRelease + ".vm" } to { buildDir + "/" + cliRelease }
    do merge { cliDev + ".vm" } to { buildDir + "/" + cliDev }

    with Travis t when path = ".travis.yml"
      begin
        do enable repo_slug github_token org
        do encrypt repo_slug github_token org { "GITHUB_TOKEN=" + github_token }
        do encrypt repo_slug github_token org { "MAVEN_USER=" + maven_user }
        do encrypt repo_slug github_token org { "MAVEN_TOKEN=" + maven_token }
      end
  end
